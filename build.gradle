plugins {
    id 'java'
    id 'jacoco'
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.10.4'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}
group = 'com.gitparse'
version = '0.3'

repositories {
    mavenCentral()
}

application {
    mainClass = 'com.gitparse.Main'
}

dependencies {
    implementation("io.javalin:javalin-bundle:6.4.0")
    implementation("io.javalin:javalin-rendering:6.4.0")
// template engine dependency
// https://mvnrepository.com/artifact/org.eclipse.jgit/org.eclipse.jgit
    implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '7.1.0.202411261347-r'
// https://mvnrepository.com/artifact/io.pebbletemplates/pebble
    implementation group: 'com.github.spullara.mustache.java', name: 'compiler', version: '0.9.10'
    implementation("com.fasterxml.jackson.core:jackson-databind:2.17.2")

    implementation("org.slf4j:slf4j-simple:2.0.11")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.14.2'
}

test {
    useJUnitPlatform()
}

graalvmNative {
    binaries.all {
        resources.autodetect()
    }
}

// Custom task to build the Docker image
task buildDockerImage(type: Exec) {
    dependsOn shadowJar
    def imageTag = "${project.version}"
    commandLine 'docker', 'buildx','build',  '--platform', 'linux/amd64', '-t', "samair/gitparse:${imageTag}", '.'
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification